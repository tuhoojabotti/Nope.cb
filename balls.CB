InitFogs:

Type FOGS
    Field img
    Field x As Float
    Field y As Float 
    Field vx As Float 
    Field vy As Float
    Field a As Float
EndType

h = LoadImage("fog.png")

count = 48
For i = 0 To count
    fog.FOGS = New(FOGS)
    fog\img = CloneImage(h)
    fog\x = Rand(sw) + (1 - Rand(1) * 2) * sw * 1.2
    fog\y = Rand(sh)
    fog\vx = Rnd(1, 3) * (1 - (fog\x > sw) * 2)
    fog\vy = Rnd(-2,2)
    fog\a = Rnd(360)
    If ScreenWidth()<>960 Or ScreenHeight()<>540 Then ResizeImage fog\img, 722*swra#, 477*shra#
    RotateImage fog\img, fog\a // Pakko k‰‰nt‰‰, muuten menee huonosti
    Text 0,0,Int((i/Float(count))*100)+"%"
    DrawScreen
Next i

Return

CalcFogs:

// Kauheeta kopiointia Wingin synkkaus esimerkist‰

If isku1<>wisku1 And runTime>9000 And (runTime<37000 Or runTime>46000) Then
    c# = 255
Else
    c# = smooth(0,c,10)
EndIf 

If isku2<>wisku2 And runTime>18000 And runTime<37000 Then
  c2# = 255
Else
  c2# = smooth(0,c2,20)
EndIf

For fog.FOGS = Each FOGS
    If runTime < 5000 Then // Slide in from the sides
        fog\x = fog\x + fog\vx * swra * multiplier
        fog\y = fog\y - Sin(fog\a) * shra * multiplier * 0.5
    ElseIf runTime > 5000 And runTime < 37000
        fog\x = fog\x + fog\vx*((c#+c2#+64)/64.0)*swra#*multiplier
        fog\y = fog\y + fog\vy*((c#+c2#+64)/64.0)*shra#*multiplier
        If fog\x>sw Or fog\x<0 Then fog\vx = -fog\vx
        If fog\y>sh Or fog\y<0 Then fog\vy = -fog\vy
    Else
        fog\x = fog\x + Cos(fog\a)*swra#*multiplier*3
        fog\y = fog\y - Sin(fog\a)*shra#*multiplier*3
    EndIf
Next fog

Return

DrawFogs:

cbeSetBlendMode(CBE_BLEND_RESET)
cbEColor(c#, c2#, 0, 200)
Box 0, 0, ScreenWidth(), ScreenHeight()

cbeSetBlendMode(CBE_BLEND_ADDITIVE)
For fog.FOGS = Each FOGS
    DrawImage fog\img, fog\x, fog\y
Next fog

Return