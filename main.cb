// Window init
Const sw = 640
Const sh = 360
swra# = ScreenWidth() / 960.0
shra# = ScreenHeight() / 540.0
SCREEN sw, sh

defaultFont = LoadFont("Courier New")

Global multiplier As Float
multiplier = 1 // Change this if the intro startTimes to stutter. :u

Include "customfunctions.cb"

// Music sync
bpm = 210
space = 60000 / bpm

// Timings
Const EFFECT_FOG        = 43000
Const EFFECT_TITLE      = 55000
Const EFFECT_EXPLOSIONS = 60000


// Effect init / precalc
Gosub InitFogs
Gosub InitExplosion
Gosub InitTitle

// Start!
PlaySound "Chipz4end.xm"
startTime = Timer()// - EFFECT_FOG // Skip to the end of the fog
lastUpdate = Timer()
t = Timer()

Repeat
    runTime = Timer() - startTime
    
    // Simulation
    While t < Timer()
        wisku1 = isku1
        isku1 = (runTime / space) / 4
        wisku2 = isku2
        isku2 = (runTime / space) / 2

        // Move to CalcFogs?
        If runTime < 18000 Then isku1 = ((runTime - 300) / space) / 4        

        If runTime < EFFECT_FOG Then Gosub CalcFogs
        If runTime > EFFECT_FOG And runTime < EFFECT_TITLE Then Gosub CalcTitle
        If runTime > EFFECT_TITLE Then Gosub CalcExplosion

        t = t + multiplier * 10
    Wend
    
    // Drawing
    If runTime < EFFECT_FOG Then Gosub DrawFogs
    If runTime > EFFECT_FOG And runTime < EFFECT_TITLE Then Gosub DrawTitle
    If runTime > EFFECT_TITLE Then Gosub DrawExplosion
    
    cbeSetBlendMode(CBE_BLEND_RESET)
    SetFont defaultFont
    Color 255, 255, 255
    Text 0, 0, FPS()
    Text 0, 15, runTime
    Text 0, 30, fadeIn#
    DrawScreen
Forever

Include "balls.cb"
Include "title.cb"
Include "explosion.cb"

Function smooth#(_new#,_old#,_smooth#)
    Return _old + (_new - _old) * (1.0 / _smooth) * multiplier
EndFunction 